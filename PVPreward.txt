// Script for PvP System. Author: Sebastian Åasisz.
// AvalonRO

-	script	PvP Coordinator	-1,{
	OnPCLogoutEvent:
		if(hidden > 0){
			set .@players$, strcharinfo(0);
			set .@player, getcharid(3);
			detachrid;
			atcommand "@unmute "+ .@players$;
			attachrid(.@player);
			atcommand "@hide";
			atcommand "@costume";
		}
		end;

	OnPCLoginEvent:
		getmapxy (.@map$, .@x, .@y, 0);
		if( .@map$ == "quiz_01" || .@map$ == "quiz_02" || .@map$ == "06guild_01" || .@map$ == "edit_map7"){
			if(hidden > 0){
				atcommand "@costume Xmas";
				atcommand "@hide";
				atcommand "@mute 3000 " + strcharinfo(0);
				detachrid;
			}
		}
		end;
}

prt_fild08,167,362,6	script	Spectator Mode	72,{
	if(getpetinfo(2) != "null"){
		mes "[Spectator Mode]";
		mes "Hello, " + strcharinfo(0) + ".";
		mes "Sorry, but " + getpetinfo(2) + " can't come with you.";
		mes "Drop him somewhere safe and talk to me again.";
		close;
	}
	if(checkhomcall() == 0){
		mes "[Spectator Mode]";
		mes "Hello, " + strcharinfo(0) + ".";
		mes "Sorry, but " + gethominfo(2) + " can't come with you.";
		mes "Drop him somewhere safe and talk to me again.";
		close;
	}
	if(getcharid(1) != 0){
		mes "[Spectator Mode]";
		mes "Hello, " + strcharinfo(0) + ".";
		mes "Sorry, but you cannot join while being in party.";
		mes "Leave your party and talk to me again.";
		close;		
	}
	mes "[Spectator Mode]";
	mes "Hello, " + strcharinfo(0) + ".";
	mes "The most glorious battle is about to begin! Oh the views... the adrenaline...";
	mes "...Oh, do you want to go? It might be dangerous!";
	next;
	switch(select("Yes:No")){
		case 1:
			mes "[Spectator Mode]";
			mes "Excellent! Have fun!";
			set .@player2, getcharid(0);
			set hidden, 1;
			undisguise;
			sc_end SC_ALL;
			atcommand "@costume Xmas";
			atcommand "@hide";
			atcommand "@mute 3000 " + strcharinfo(0);
			detachrid;
			switch($pvp_map){
				case 1:
					warpchar "quiz_01",63,183,.@player2;
					close;
				case 2:
					warpchar "06guild_01",49,49,.@player2;
					close;
				case 3:
					warpchar "quiz_02",304,249,.@player2;
					close;
				case 4:
					warpchar "edit_map7",50,50,.@player2;
					close;
			}
			end;	
		case 2:
			mes "[Spectator Mode]";
			mes "You got no idea what you're going to miss...";
			mes "Oh well...farewell my friend, farewell";
			close;
	}
}

quiz_01,63,195,6	script	Exit Arena	49,{
	if(select("No:Yes")==1){
		close;
	}
	if(hidden > 0){
		set .@players$, strcharinfo(0);
		set .@player, getcharid(3);
		detachrid;
		atcommand "@unmute "+ .@players$;
		attachrid(.@player);
		atcommand "@hide";
		atcommand "@costume";
		set hidden, 0;
	}
	warp getsavepoint(0),getsavepoint(1),getsavepoint(2);
	end;
}

06guild_01,50,95,6	duplicate(Exit Arena)	Exit Arena#2	49
quiz_02,303,230,6	duplicate(Exit Arena)	Exit Arena#3	49
edit_map7,49,100,6	duplicate(Exit Arena)	Exit Arena#4	49

lhz_que01,86,165,5	script	Event Assistant	811,{
	if(getgmlevel() > 51){
		mes "[Event Assistant]";
		mes "Hello, " + strcharinfo(0) + " I can activate pvp script for you";
		next;
		mes "[Event Assistant]";
		mes "What do you want to do?";
		next;
		switch(select("Set restrictions:Set map:Enable Tom:Enable Spectator Mode:Close Event:Nothing")){
			case 1:
				mes "[Event Assistant]";
				mes "Which NPC version do you want?";
				switch(select("General PvP:General PvP(no ValkC):General PVP(No HwC,FBHC,ValkC):Magic Classes:None")){
					case 1:
						set $pvp_npc, 1;
						close;
					case 2:
						set $pvp_npc, 2;
						close;
					case 3:
						set $pvp_npc, 3;
						close;
					case 4:
						set $pvp_npc, 4;
						close;
					case 5:
						close;
				}		
			case 2:
				mes "[Event Assistant]";
				mes "Which map do you want to enable?";
				switch(select("quiz_01:06guild_01:quiz_02:Jette's Map:None")){
					case 1:
						set $pvp_map, 1;
						close;
					case 2:
						set $pvp_map, 2;
						close;
					case 3:
						set $pvp_map, 3;
						close;
					case 4:
						set $pvp_map, 4;
						close;
					case 5:
						close;
				}
			case 3:	
				mes "[Event Assistant]";
				mes "I've enabled Tom";
				enablenpc "Tom";
				close;
			case 4:	
				mes "[Event Assistant]";
				mes "I've enabled Spectator Mode";
				enablenpc "Spectator Mode";
				close;
			case 5:
				mes "[Event Assistant]";
				mes "Thank you for abusing me, you bastard!";
				disablenpc "Tom";
				disablenpc "Spectator Mode";
				close;
			case 6:
				close;
		}
	}
	else{
		mes "[Event Assistant]";
		mes "Hello, " + strcharinfo(0) + ".";
		mes "Sorry, but don't have powera to activate the npc.";
		close;
	}
	
	OnInit:
		disablenpc "Tom";
		disablenpc "Spectator Mode";
		setmapflag "quiz_01", mf_partylock;
		setmapflag "quiz_01", mf_guildlock;
		setmapflag "quiz_01", mf_notrade;
		setmapflag "quiz_01", mf_noreturn;
		setmapflag "quiz_01", mf_nobranch;
		setmapflag "quiz_01", mf_pvp_noguild;
		setmapflag "quiz_01", mf_nomemo;
		setmapflag "quiz_01", mf_noteleport;
		setmapflag "quiz_01", mf_nochat;
		setmapflag "quiz_01", mf_nodrop;
		setmapflag "quiz_01", mf_nocommand,40;
		setmapflag "quiz_02", mf_partylock;
		setmapflag "quiz_02", mf_guildlock;
		setmapflag "quiz_02", mf_notrade;
		setmapflag "quiz_02", mf_noreturn;
		setmapflag "quiz_02", mf_nobranch;
		setmapflag "quiz_02", mf_pvp_noguild;
		setmapflag "quiz_02", mf_nomemo;
		setmapflag "quiz_02", mf_noteleport;
		setmapflag "quiz_02", mf_nochat;
		setmapflag "quiz_02", mf_nodrop;
		setmapflag "quiz_02", mf_nocommand,40;
		setmapflag "06guild_01", mf_partylock;
		setmapflag "06guild_01", mf_guildlock;
		setmapflag "06guild_01", mf_notrade;
		setmapflag "06guild_01", mf_noreturn;
		setmapflag "06guild_01", mf_nobranch;
		setmapflag "06guild_01", mf_pvp_noguild;
		setmapflag "06guild_01", mf_nomemo;
		setmapflag "06guild_01", mf_noteleport;
		setmapflag "06guild_01", mf_nochat;
		setmapflag "06guild_01", mf_nodrop;
		setmapflag "06guild_01", mf_nocommand,40;
		end;
}

z_agit,24,187,6	script	Tom	70,{
	mes "[Tom]";
	mes "Hello. I'm Tom and I'm event helper";
	mes "I will decide if you are eligible to join this event.";
	next;
	for( set .@i, 501; .@i <= 599; set .@i, .@i +1 ){
		if(countitem(.@i) > 0 && .@i != 523){
			mes "[Tom]";
			mes "Oi! You can't bring " + getitemname(.@i) + " with you.";
			mes "YOU ARE NOT PREPARED!!";
			close;
		}
	}
	for( set .@i, 0; .@i < getarraysize(.@consumables); set .@i, .@i +1 ){
		if(countitem(.@consumables[.@i]) > 0){
			mes "[Tom]";
			mes "Oi! You can't bring " + getitemname(.@consumables[.@i]) + " with you.";
			mes "YOU ARE NOT PREPARED!!";
			close;
		}
	}
	mes "[Tom]";
	mes "Oh, you've passed all restrictions";
	next;
	mes "[Tom]";
	mes "Do you want to go?";
	switch(select("Yes:No")){
		case 1:
			mes "[Tom]";
			mes "I will teleport you to event map now.";
			mes "Get ready! Geronimoooooooooooooo!!";
			switch($pvp_map){
				case 1:
					warp "quiz_01",63,165;
					close;
				case 2:
					warp "06guild_01",49,49;
					close;
				case 3:
					warp "quiz_02",304,249;
					close;
				case 4:
					warp "edit_map7",50,50;
					close;
			}
			end;	
		case 2:
			mes "[Tom]";
			mes "You got no idea what you're going to miss...";
			mes "Oh well...farewell my friend, farewell";
			close;
	}
}

06guild_01,50,50,5	script	Helper	1187,{
	end;
	Part1:
		if(getgmlevel() < 51){
			end;
		}
		areamonster "06guild_01",25,53,25,46,"Wall",1905,40;
		areamonster "06guild_01",74,53,74,46,"Wall",1905,40;
		areamonster "06guild_01",46,74,53,74,"Wall",1905,40;
		areamonster "06guild_01",46,25,53,25,"Wall",1905,40;
		setwall "06guild_01",25,53,8,4,0,"wall01";
		setwall "06guild_01",74,53,8,4,0,"wall02";
		setwall "06guild_01",46,74,8,6,0,"wall03";
		setwall "06guild_01",46,25,8,6,0,"wall04";
		dispbottom "Walls has been enabled.";
		end;

	Part2:
		if(getgmlevel() < 51){
			end;
		}
		delwall "wall01";
		delwall "wall02";
		delwall "wall03";
		delwall "wall04";
		killmonsterall "06guild_01";
		dispbottom "Walls has been disabled.";
		end;

	Part3:
		if(getgmlevel() < 51){
			end;
		}
		dispbottom "Hello, this is help panel.";
		sleep2 2000;
		dispbottom "I will guide you through the NPC.";
		sleep2 2000;
		dispbottom "There's 3 avaiable commands for now: /help/, ";
		sleep2 2000;
		dispbottom "which you just activated. Also there's /set walls/, ";
		sleep2 2000;
		dispbottom "which activate walls. And there's /remove walls/, ";
		sleep2 2000;
		dispbottom "which will remove all active walls.";
		sleep2 4000;
		dispbottom "Remember to firstly remove all walls and next to activate them.";
		sleep2 2000;
		dispbottom "That's all for now. Enjoy.";
		end;

	OnInit:
		defpattern 1, "set walls", "Part1";
		defpattern 1, "remove walls", "Part2";
		defpattern 1, "help", "Part3";
		activatepset 1;
		end;
}

-	script	EquipCheck	-1,{
 	OnPCLoadMapEvent:
 		setarray .@restrictedmaps$[0], "quiz_01","quiz_02","06guild_01","quiz_test","bob04","guild_vs5","edit_map7";
 		switch($pvp_npc){
 			case 0:
 				setarray .@itemsnotallowed[0],0;
 				setarray .@cardsnotallowed[0],0;
 				break;
 			case 1:
 				setarray .@itemsnotallowed[0],2541,2382,1530,1132,1565,1369,1141,2629,2545,2383,1579;
 				setarray .@cardsnotallowed[0],4128,4276,4361;
 				break;
 			case 2:
 				setarray .@itemsnotallowed[0],2541,2382,1530,1132,1565,1369,1141,2629,2545,2383,1579;
 				setarray .@cardsnotallowed[0],4407,4128,4276,4361;
 				break;
 			case 3:
 				setarray .@itemsnotallowed[0],2541,2382,1530,1132,1565,1369,1141,2629,2545,2383,1579;
 				setarray .@cardsnotallowed[0],4441,4365,4407,4128,4276,4361;
 				break;
 			case 4:
 				setarray .@itemsnotallowed[0],28209,28218,28105,28106,5058,5230,5231,5232,5233,5234,5219,5124,2541,2382,1530,1132,1565,1369,1141,2629,2545,2383,1579;
 				setarray .@cardsnotallowed[0],4146,4414,4087,4441,4365,4128,4276,4361,4128,4276,4361;
 				break;
 		}
 		getmapxy(.@map$,.@x,.@y,0);
 		for(set .@m, 0; .@m < getarraysize(.@restrictedmaps$); set .@m, .@m + 1) {
 			while(.@map$ == .@restrictedmaps$[.@m]){
 				for(set .@e, 1; .@e < 11; set .@e, .@e + 1) {
 					if(getequipid(.@e) != -1) {
 						for(set .@n, 0; .@n < getarraysize(.@cardsnotallowed); set .@n, .@n + 1){
	 						if(getequipcardid(.@e, 0) == .@cardsnotallowed[.@n] || getequipcardid(.@e, 1) == .@cardsnotallowed[.@n] || getequipcardid(.@e, 2) == .@cardsnotallowed[.@n] || getequipcardid(.@e, 3) == .@cardsnotallowed[.@n]){
	 							dispbottom "Item '"+getitemname(getequipcardid(.@e, .@b))+"' is not allowed in this map. It has been unequiped.";
	 							unequip .@e;
	 							break;
	 						}
	 					}
	 					for(set .@i, 0; .@i < getarraysize(.@itemsnotallowed); set .@i, .@i + 1) {
	 						if(getequipid(.@e) == .@itemsnotallowed[.@i]) {
	 							dispbottom "Item '"+getitemname(getequipid(.@e))+"' is not allowed in this map. It has been unequiped.";
	 							unequip .@e;
	 							break; 							
	 						}				
	 					}
 					}
 				}
 				sleep2 100;
 				getmapxy(.@map$,.@x,.@y,0);
 			}
 		}
 		end;
}

06guild_01	mapflag	loadevent
quiz_01	mapflag	loadevent
quiz_02	mapflag	loadevent